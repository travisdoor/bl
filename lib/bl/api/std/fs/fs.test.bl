#import "std/test"
#import "std/debug_allocator"

#import "std/fs"

main :: fn () s32 {
    dbgalloc_init();
    defer dbgalloc_terminate();
    return test_run();    
}

#private
get_dummy_filepath :: fn () string {
    tmp :: fs_get_tmp();
    string_concatenate(&tmp, "/deleteme.txt");
    return tmp;
}

fs_exist_test :: fn () #test {
    test_true(fs_exist(#file));
    test_false(fs_exist("foobar"));
}

fs_get_home_test :: fn () #test {
    tmp :: fs_get_home();
    defer string_delete(tmp);
    test_neq(tmp, string_empty);
    test_true(fs_exist(tmp));
    test_true(tmp[tmp.len-1] != '/');

    state, err :: fs_is_directory(tmp);
    test_ok(err);
    test_true(state);
}

fs_get_tmp_test :: fn () #test {
    tmp :: fs_get_tmp();
    defer string_delete(tmp);
    test_neq(tmp, string_empty);
    test_true(fs_exist(tmp));    
    test_true(tmp[tmp.len-1] != '/');

    state, err :: fs_is_directory(tmp);
    test_ok(err);
    test_true(state);
}

fs_get_cwd_test :: fn () #test {
    tmp :: fs_get_cwd();
    defer string_delete(tmp);
    test_neq(tmp, string_empty);
    test_true(fs_exist(tmp));    
    test_true(tmp[tmp.len-1] != '/');

    state, err :: fs_is_directory(tmp);
    test_ok(err);
    test_true(state);
}

fs_open_test1 :: fn () #test {
    file, err :: fs_open("");
    defer fs_close(file);
    test_is_error(err, ERR_INVALID_HANDLE);    
}
    
fs_open_test2 :: fn () #test {
    file, err :: fs_open("foobar");
    defer fs_close(file);
    test_is_error(err, ERR_NOT_FOUND);
}
    
fs_open_test3 :: fn () #test {
    file, err :: fs_open(#file);    
    defer fs_close(file);
    test_ok(err);

    // Cannot write to file when Write or Append is not specified.
    written, err2 :: fs_write_string(file, "Hello!");
    test_not_ok(err2);
    test_eq(written, 0);
}

fs_file_create_test1 :: fn () #test {
    file, err :: fs_open("", FSOpenMode.Create);
    defer fs_close(file);
    test_is_error(err, ERR_INVALID_HANDLE);    
}

fs_file_create_test2 :: fn () #test {
    filepath :: get_dummy_filepath();
    defer string_delete(filepath);
    fs_remove(filepath);

    {
        file, err :: fs_open(filepath, FSOpenMode.Create);
        defer fs_close(file);
        test_ok(err);

        // Cannot write to file when Write or Append is not specified.
        written, err2 :: fs_write_string(file, "Hello!");
        test_not_ok(err2);
        test_eq(written, 0);
    }    

    {
        file, err :: fs_open(filepath, FSOpenMode.Create, FSOpenMode.Write);
        defer fs_close(file);
        test_ok(err);

        written, err2 :: fs_write_string(file, "Hello!");
        test_ok(err2);
        test_neq(written, 0);
    }    

    { // This should reuse already created file!
        file, err :: fs_open(filepath, FSOpenMode.Create);
        defer fs_close(file);
        test_ok(err);
        str :: fs_read_string(file);
        test_eq(str, "Hello!");
    }
    fs_remove(filepath);
}

fs_create_append :: fn () #test {
    filepath :: get_dummy_filepath();
    defer string_delete(filepath);
    fs_remove(filepath);
    { // initial create
        file, err :: fs_open(filepath, FSOpenMode.Create, FSOpenMode.Write);
        defer fs_close(file);
        test_ok(err);
        fs_write_string(file, "Hello!");
    }
    { // append
        file, err :: fs_open(filepath, FSOpenMode.Append);
        defer fs_close(file);
        test_ok(err);
        fs_write_string(file, "Hello!");
    }
    { // check
        file, err :: fs_open(filepath);
        defer fs_close(file);
        test_ok(err);
        str, err2 :: fs_read_string(file);
        test_ok(err2);
        test_eq(str, "Hello!Hello!");
    }
    fs_remove(filepath);
}

fs_read_string_test :: fn () #test {
    file, err1 :: fs_open(#file, FSOpenMode.Read);
    defer fs_close(file);
    test_ok(err1);
    data, err2 := fs_read_string(file);
    test_ok(err2);
    defer string_delete(data);
    test_neq(data, string_empty);
    data.len += 1;
    test_true(data[data.len-1] == '\0');
}

fs_read_string_test2 :: fn () #test {
    data, err := fs_read_string(FS_FILE_INVALID);
    test_is_error(err, ERR_INVALID_HANDLE);
}

fs_read_slice_test :: fn () #test {
    file :: fs_open(#file, FSOpenMode.Read);
    defer fs_close(file);
    data, err := fs_read_slice(file);
    test_ok(err);
    defer slice_terminate(data);
    test_not_null(data.ptr);
    test_neq(data.len, 0);
}

fs_validate_filename_test :: fn () #test {
    if OS_KIND == OSKind.Windows {
        test_false(fs_validate_filename("<"));
        test_false(fs_validate_filename(">"));
        test_false(fs_validate_filename(":")); 
        test_false(fs_validate_filename("\""));     
        test_false(fs_validate_filename("/"));
        test_false(fs_validate_filename("\\"));
        test_false(fs_validate_filename("|")); 
        test_false(fs_validate_filename("?"));        
        test_false(fs_validate_filename("*"));
        test_false(fs_validate_filename("\t"));
        test_false(fs_validate_filename("foo<"));
        test_true(fs_validate_filename("foo_bar-File.txt"));
    } else {
        test_false(fs_validate_filename("foo/"));
        test_true(fs_validate_filename("foo_bar-File.txt"));
    }
}

fs_get_extension_test :: fn () #test {
    {
        filename :: "foo.bar";
        ext      :: fs_get_extension(filename);
        test_eq(ext, "bar");
    }
    {
        filename :: "foo.bar.bar";
        ext      :: fs_get_extension(filename);
        test_eq(ext, "bar.bar");
    }
    {
        filename :: "";
        ext      :: fs_get_extension(filename);
        test_eq(ext, string_empty);
    }    
    {
        filename :: ".";
        ext      :: fs_get_extension(filename);
        test_eq(ext, string_empty);
    }
    {
        filename :: ".foo";
        ext      :: fs_get_extension(filename);
        test_eq(ext, string_empty);
    }
    {
        filename :: ".foo.bar";
        ext      :: fs_get_extension(filename);
        test_eq(ext, "bar");
    }
    {
        filename :: ".foo.bar.bar";
        ext      :: fs_get_extension(filename);
        test_eq(ext, "bar.bar");
    }        
}

fs_remove_extension_test :: fn () #test {
    {
        filename :: "foo.bar";
        ext      :: fs_remove_extension(filename);
        test_eq(ext, "foo");
    }
    {
        filename :: "foo.bar.bar";
        ext      :: fs_remove_extension(filename);
        test_eq(ext, "foo");
    }
    {
        filename :: "";
        ext      :: fs_remove_extension(filename);
        test_eq(ext, string_empty);
    }    
    {
        filename :: ".";
        ext      :: fs_remove_extension(filename);
        test_eq(ext, ".");
    }
    {
        filename :: ".foo";
        ext      :: fs_remove_extension(filename);
        test_eq(ext, ".foo");
    }
    {
        filename :: ".foo.bar";
        ext      :: fs_remove_extension(filename);
        test_eq(ext, ".foo");
    }
    {
        filename :: ".foo.bar.bar";
        ext      :: fs_remove_extension(filename);
        test_eq(ext, ".foo");
    }
}

fs_normalize_test1 :: fn () #test {
    cwd :: fs_get_cwd();
    defer string_delete(cwd);
    parent: string;
    test_true(string_split_by_last(cwd, '/', &parent, null));
    filepath :: string_new("..");
    defer string_delete(filepath);
    status :: fs_normalize(&filepath);
    test_ok(status);
    test_eq(filepath, parent);
}

fs_normalize_test2 :: fn () #test {
    cwd :: fs_get_cwd();
    defer string_delete(cwd);
    filepath :: string_new(".");
    defer string_delete(filepath);
    status :: fs_normalize(&filepath);
    test_ok(status);
    test_eq(filepath, cwd);
}    

fs_normalize_test3 :: fn () #test {
    file :: fs_open("foo", FSOpenMode.Create);
    fs_close(file);
    defer fs_remove("foo");
    cwd :: fs_get_cwd();
    defer string_delete(cwd);    
    filepath :: string_new("foo");
    string_concatenate(&cwd, "/", filepath);
    defer string_delete(filepath);
    status :: fs_normalize(&filepath);
    test_ok(status);
    test_eq(filepath, cwd);
}

fs_remove_test :: fn () #test {
    filepath :: get_dummy_filepath();
    defer string_delete(filepath);
    
    fs_remove(filepath);
    file, err :: fs_open(filepath, FSOpenMode.Create);
    test_ok(err);
    fs_close(file);
    err2 :: fs_remove(filepath);
    test_ok(err2);
}

fs_size_test :: fn () #test {
    file :: fs_open(#file);
    defer fs_close(file);
    size, err :: fs_size(file);
    test_ok(err);
    test_true(size > 0);
}

fs_read_test :: fn () #test {
    file :: fs_open(#file);
    defer fs_close(file);
    buf: [1042]u8 #noinit;
    bytes, err :: fs_read(file, buf.ptr, buf.len);
    test_ok(err);
    test_eq(bytes, buf.len);
}

fs_read_buffer_test2 :: fn () #test {
    buf: [1042]u8 #noinit;
    bytes, err :: fs_read(FS_FILE_INVALID, buf.ptr, buf.len);
    test_is_error(err, ERR_INVALID_HANDLE);
    test_eq(bytes, 0);
}

fs_read_buffer_test3 :: fn () #test {
    file :: fs_open(#file);
    defer fs_close(file);
    bytes, err :: fs_read(file, null, 0);
    test_is_error(err, ERR_INVALID_HANDLE);
    test_eq(bytes, 0);
}

fs_read_test4 :: fn () #test {
    file :: fs_open(#file);
    defer fs_close(file);
    buf: [1024]u8 #noinit;
    bytes, err :: fs_read(file, buf.ptr, 100);
    test_ok(err);
    test_eq(bytes, 100);
}

fs_write_test :: fn () #test {
    filepath :: get_dummy_filepath();
    defer string_delete(filepath);
    fs_remove(filepath);
    content :: "Hello I'm cool file content!";    
    
    { // write
        file :: fs_open(filepath, FSOpenMode.Create, FSOpenMode.Write);
        written, err :: fs_write(file, content.ptr, content.len);
        test_ok(err);
        test_eq(written, content.len);
        fs_close(file);
    }
    { // read
        file :: fs_open(filepath);
        tmp, err :: fs_read_string(file);
        test_ok(err);
        defer string_delete(tmp);
        test_eq(tmp, content);
        fs_close(file);
    }
    fs_remove(filepath);
}

fs_write_string_test :: fn () #test {
    filepath :: get_dummy_filepath();
    defer string_delete(filepath);
    fs_remove(filepath);
    content :: "Hello I'm cool file content!";    
    
    { // write
        file :: fs_open(filepath, FSOpenMode.Create, FSOpenMode.Write);
        written, err :: fs_write_string(file, content);
        test_ok(err);
        test_eq(written, content.len);
        fs_close(file);
    }
    { // read
        file :: fs_open(filepath);
        tmp, err :: fs_read_string(file);
        test_ok(err);
        defer string_delete(tmp);
        test_eq(tmp, content);
        fs_close(file);
    }
    fs_remove(filepath);
}

fs_write_slice_test :: fn () #test {
    filepath :: get_dummy_filepath();
    defer string_delete(filepath);
    fs_remove(filepath);
    content :: {:[5]u8: 'H', 'e', 'l', 'o', 'o' };
    
    { // write
        file :: fs_open(filepath, FSOpenMode.Create, FSOpenMode.Write);
        written, err :: fs_write_slice(file, content);
        test_ok(err);
        test_eq(written, content.len);
        fs_close(file);
    }
    { // read
        file :: fs_open(filepath);
        tmp, err :: fs_read_string(file);
        test_ok(err);
        defer string_delete(tmp);
        test_eq(tmp.len, content.len);
        fs_close(file);
    }
    fs_remove(filepath);
}

fs_list_dir_test1 :: fn () #test {
    dir :: fs_get_cwd();
    defer string_delete(dir);
    list, err :: fs_dir_scan(dir);
    defer fs_info_list_delete(list);
    test_ok(err);
    test_true(list.len > 0);
    loop i := 0; i < list.len; i += 1 {
        test_neq(list[i].name, string_empty);
    }
}

fs_list_dir_test2 :: fn () #test {
    dir :: fs_get_cwd();
    defer string_delete(dir);
    list, err :: fs_dir_scan(dir, &fn (info: *FSInfo) bool {
        return info.kind == FSInfoKind.Directory;
    });
    defer fs_info_list_delete(list);
    test_ok(err);
    test_true(list.len > 0);
    loop i := 0; i < list.len; i += 1 {
        test_neq(list[i].name, string_empty);
        test_true(list[i].kind == FSInfoKind.Directory);
    }
}

fs_list_dir_test3 :: fn () #test {
    dir :: fs_get_cwd();
    defer string_delete(dir);
    list, err :: fs_dir_scan(dir, &fn (info: *FSInfo) bool {
        return info.kind == FSInfoKind.File;
    });
    defer fs_info_list_delete(list);
    test_ok(err);
    test_true(list.len > 0);
    loop i := 0; i < list.len; i += 1 {
        test_neq(list[i].name, string_empty);
        test_true(list[i].kind == FSInfoKind.File);
    }
}

fs_is_directory_test1 :: fn () #test {
    tmp :: fs_get_tmp();
    defer string_delete(tmp);
    state, err :: fs_is_directory(tmp);
    test_ok(err);
    test_true(state);
}

fs_is_directory_test2 :: fn () #test {
    state, err :: fs_is_directory(#file);
    test_ok(err);
    test_false(state);
}

fs_dir_create_remove_test1 :: fn () #test {
    dir :: fs_get_tmp();
    defer string_delete(dir);
    string_append(&dir, "/__DELETE_ME__");

    fs_dir_remove_all(dir);
    state1 :: fs_dir_create(dir);
    test_ok(state1);

    state2 :: fs_dir_create(dir);
    test_not_ok(state1);

    state3 :: fs_dir_remove(dir);
    test_ok(state3);
}

fs_dir_remove_all_test1 :: fn () #test {
    dir :: fs_get_tmp();
    defer string_delete(dir);
    string_append(&dir, "/__DELETE_ME__");
    
    fs_dir_remove_all(dir);
    {
        s :: fs_dir_create(dir);
        test_ok(s);
    }
    
    {
        file :: string_new(dir);
        defer string_delete(file);
        string_append(&file, "/file.txt");
        f, e :: fs_open(file, FSOpenMode.Create);
        test_ok(e);
        fs_close(f);
    }
    
    state :: fs_dir_remove_all(dir);
    test_ok(state);
    test_false(fs_exist(dir));
}