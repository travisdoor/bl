#private
function_calls :: fn () #test {
    _1 :: fn () {
    };

    _2 :: fn (i: s32) s32 {
        return i;
    };

    _1();  
    assert(_2(10) == 10);
};

implicit_functions_direct_call :: fn () #test {
    i := fn (i: s32, j: s32) s32 {
        return i * j; 
    } (2, 3);

    assert(i == 6);

    fn () {
    } ();
};

function_pointers :: fn () #test {
    foo :: fn (v: s32) s32 {
        return v;
    };

    bar :: fn (v: s32) s32 {
        return v;
    };

    ptr := &foo;
    assert(ptr(10) == 10);

    ptr = &bar;
    assert(ptr(20) == 20);

    ptr2 := &fn () {};
    ptr2();
};

nested_function_pointers :: fn () #test {
    Foo :: struct {
        v_fn: *fn()
    };

    TestFn :: fn () {};

    foo := Foo.{};
    foo.v_fn = &TestFn; 

    foo.v_fn();
};

multiple_return1 :: fn () #test {
    foo :: fn () (s32, s32) {
        return 10, 20;
    };
    a, b :: foo();
    test_eq(a, 10);
    test_eq(b, 20);
}

multiple_return2 :: fn () #test {
    foo :: fn () (s32, s32) {
        return 10, 20;
    };
    a :: foo();
    test_eq(a, 10);
}

multiple_return3 :: fn () #test {
    foo :: &fn () (s32, s32) {
        return 10, 20;
    };
    a, b :: foo();
    test_eq(a, 10);
    test_eq(b, 20);
}

multiple_return4 :: fn () #test {
    foo :: fn () (s32, s32) {
        return 10, 20;
    };

    bar :: fn () (s32, s32) {
        return foo();
    };

    a, b :: bar();
    test_eq(a, 10);
    test_eq(b, 20);
}